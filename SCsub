#!/usr/bin/env python
Import('env')

env_module = env.Clone()

thirdparty_dir = 'thirdparty/anl/'
thirdparty_dirs = [
    thirdparty_dir + 'VM/',
    thirdparty_dir + 'Expression/',
    thirdparty_dir + 'Imaging/',
    thirdparty_dir + 'Processing/',
]
env_module.Append(CPPPATH=thirdparty_dirs)

# ANL sources
env_thirdparty = env_module.Clone()
for d in thirdparty_dirs:
    env_thirdparty.add_source_files(env.modules_sources, d + '*.cpp')
    
# Suppress warnings
env_thirdparty.disable_warnings()
if env.msvc:
    suppress_multiple_copy_constructors = '/wd4521'
    env_thirdparty.Append(CXXFLAGS=[suppress_multiple_copy_constructors])
    
if env["anl_use_long_period"]:
    env_module.Append(CPPDEFINES=["ANL_LONG_PERIOD_HASHING"])

if env["anl_use_expressions_camelcase"]:
    env_module.Append(CPPDEFINES=["ANL_EXPRESSION_BUILDER_CAMELCASE"])

# Module sources
env_module.add_source_files(env.modules_sources, '*.cpp')
env_module.add_source_files(env.modules_sources, 'plugins/*.cpp')

Export('env_module')
